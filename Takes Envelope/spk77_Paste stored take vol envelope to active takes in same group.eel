/*
   * ReaScript Name: Paste stored take vol envelope to active takes in same group
   * EEL script for Cockos REAPER
   * Author: spk77
   * Author URI: http://forum.cockos.com/member.php?u=49553
   * Licence: GPL v3
   * Version: 1.0
   */
  
  // Paste "stored" take volume envelope to active takes in same group
// EEL script by spk77 16.7.2014
//
// Version: 0.2014.7.16

function paste_take_vol_env() local(source_take, group, source_vol_env, item_i, item, env)
(
  GetExtState(#retval, "take_envelope_source", "take");
  match("%d", #retval, source_take) ? (
    // if source_take is not in grouped item, show error message
    (group = GetMediaItemInfo_Value(GetMediaItemTake_Item(source_take), "I_GROUPID")) ? ( 
      (source_vol_env = GetTakeEnvelopeByName(source_take, "Volume")) ? (
        GetSetEnvelopeState(source_vol_env, #source_vol_env);
        
        Undo_BeginBlock();
        item_i = 0;
        group > 0 && loop(CountMediaItems(0),
          item = GetMediaItem(0, item_i);
          GetMediaItemInfo_Value(item, "I_GROUPID") == group && GetActiveTake(item) != source_take ? (
            (env = GetTakeEnvelopeByName(GetActiveTake(item), "Volume")) ? (
              GetSetEnvelopeState(env, #source_vol_env);
            ) : (
              isMediaItemSelected(item) ? (
                Main_OnCommand(NamedCommandLookup("_S&M_TAKEENV1"), 0);
              ) : (
                SetMediaItemSelected(item, 1);
                UpdateArrange();
                Main_OnCommand(NamedCommandLookup("_S&M_TAKEENV1"), 0);
                SetMediaItemSelected(item, 0);
              );
              (env = GetTakeEnvelopeByName(GetActiveTake(item), "Volume")) ? (
                GetSetEnvelopeState(env, #source_vol_env);
              );
            );
          );
          item_i += 1;
        );
        Undo_EndBlock("Paste take vol envelope", -1);
      );
    ) : (
      ShowConsoleMsg("Stored take's parent item is not grouped");
    );
  );
);

paste_take_vol_env();
