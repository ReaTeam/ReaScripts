/*
   * ReaScript Name: Convert all takes rate to pitch
   * EEL script for Cockos REAPER
   * Author: Elan Hickler (Argitoth)
   * Author URI: http://forum.cockos.com/member.php?u=7973
   * Licence: GPL v3
   * Version: 1.0
*/
  
  // Convert item rate to pitch

// by Elan Hickler
// www.Soundemote.com
// www.elanhickler.com

Undo_BeginBlock();

items = CountSelectedMediaItems(0);
i=0;
loop(items,
  item        = GetSelectedMediaItem(0,i);
  snap_offset = GetMediaItemInfo_Value(item, "D_SNAPOFFSET");
  length      = GetMediaItemInfo_Value(item, "D_LENGTH");
  actTake     = GetActiveTake(item);
  rate        = GetMediaItemTakeInfo_Value(actTake, "D_PLAYRATE");
  pitch       = GetMediaItemTakeInfo_Value(actTake, "D_PITCH");
  
  // convert rate to pitch and reset rate
  newRate = 1;
  newPitch = (12/log(2)) * log(rate);
    
  takes = CountTakes(item);
  t=0;
  loop(takes,
    take = GetTake(item, t);
    
    // set new pitch
    SetMediaItemTakeInfo_Value(take, "D_PITCH", newPitch);  
    
    // set new rate
    SetMediaItemTakeInfo_Value(take, "D_PLAYRATE", newRate);
    
    // turn off preserve pitch
    SetMediaItemTakeInfo_Value(take, "B_PPITCH", 0);
    
    t+=1;  
  );
  // set new snap offset
  snap_offset > 0 ? (
    new_snap_offset = snap_offset * rate;
    SetMediaItemInfo_Value(item, "D_SNAPOFFSET", new_snap_offset);
  );
  
  // set new length
  newLength = rate / newRate * length;
  SetMediaItemLength(item, newLength, 0);
    
  i+=1;
);

UpdateArrange();

Undo_EndBlock("Convert item rate to pitch", -1);
