// @description Item stretch list in time selection
// @author Damien Stewart (Hypex)
// @version 1.0
// @screenshot Example https://github.com/Hypexed/Reaper-Scripts/raw/main/StretchList.png
// @about
//   # Item Stretch List
//
//   Displays a list of stretch markers in active take of selected item.
//
//   Will show table with Marker, Time, Position, Source-Offset and Slope.
//
//   Marker: Index in list.
//   Time: On timeline.
//   Position: In take.
//   Source-Offset: In source media.
//   Slope: Value if any.

function List()
(
    Item = GetSelectedMediaItem(0, 0);
    ItemStart = GetMediaItemInfo_Value(Item, "D_POSITION");
    Take = GetActiveTake(Item);
    TakeOffset = GetMediaItemTakeInfo_Value(Take, "D_STARTOFFS");
    Markers = GetTakeNumStretchMarkers(Take);
    
    Markers ?
    (
        #List = "Legend: M=Marker, T=Time, P=Position, O=Source-Offset, S=Slope\n\n";
        while(Marker < Markers)
        (
            GetTakeStretchMarker(Take, Marker, Position, Offset);
            (
                Time = ItemStart + Position;
                Slope = GetTakeStretchMarkerSlope(Take, Marker);
                sprintf(#Info, "M=%d, T=%.9f, P=%.9f, O=%.9f, S=%f\n", Marker, Time, Position, Offset, Slope);
                #List += #Info;
            );
            
            Marker += 1;
        );
        
        ShowMessageBox(#List, #Title, 0);

    )
    :
    (
        ShowMessageBox("Info: No stretch markers exist in active take of item.", #Title, 0);
    );
);

function Info()
(
    ShowMessageBox("Instructions: Select an item to see list of stretch markers in active take of item.", #Title, 0);
);

function main()
(
    #Title="Stretch Marker List";
    
    CountSelectedMediaItems(0) ?
    (
        List();
    )
    :
    (
        Info();
    );
);

main();

