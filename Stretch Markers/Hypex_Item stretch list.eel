// @description Item stretch list
// @author Damien Stewart (Hypex)
// @version 1.1
// @changelog
//   Add Hypex_Item stretch list in time selection.eel
//   Updated to only list stretch markers within item time and format time as standard.
// @provides [main] Hypex_Item stretch list/Hypex_Item stretch list in time selection.eel
// @screenshot Example https://github.com/Hypexed/Reaper-Scripts/raw/main/StretchList/StretchList.png
// @about
//   ### Item Stretch List
//
//   Displays a list of stretch markers in active take of selected item.
//
//   Will show table with Marker, Time, Position, Source-Offset and Slope.
//
//   Marker: Index in list.  
//   Time: On timeline.  
//   Position: In take.  
//   Source-Offset: In source media.  
//   Slope: Value if any.

function List()
(
    Item = GetSelectedMediaItem(0, 0);
    ItemStart = GetMediaItemInfo_Value(Item, "D_POSITION");
    ItemLength = GetMediaItemInfo_Value(Item, "D_LENGTH");
    ItemEnd = ItemStart + ItemLength;
    Take = GetActiveTake(Item);
    TakeOffset = GetMediaItemTakeInfo_Value(Take, "D_STARTOFFS");
    Markers = GetTakeNumStretchMarkers(Take);

    Count = 0;

    Markers ?
    (
        #List = "Legend: M=Marker, T=Time, P=Position, O=Source-Offset, S=Slope\n\n";
        while(Marker < Markers)
        (
            GetTakeStretchMarker(Take, Marker, Position, Offset);
            (
                Time = ItemStart + Position;

                (Time >= ItemStart) && (Time <= ItemEnd) ?
                (
                    format_timestr(Time, #Time);
                    Slope = GetTakeStretchMarkerSlope(Take, Marker);
                    sprintf(#Info, "M=%d, T=%s, P=%.10f, O=%.10f, S=%+.10f\n", Marker, #Time, Position, Offset, Slope);
                    #List += #Info;
                    Count += 1;
                )
            );

            Marker += 1;
        );

        Count ?
        (
            ShowMessageBox(#List, #Title, 0);
        )
        :
        (
            ShowMessageBox("Info: No stretch markers found within item time.", #Title, 0);
        );

    )
    :
    (
        ShowMessageBox("Info: No stretch markers exist in active take of item.", #Title, 0);
    );
);

function Info()
(
    ShowMessageBox("Instructions: Select an item to see list of stretch markers in active take of item.", #Title, 0);
);

function main()
(
    #Title="Stretch Marker List";

    CountSelectedMediaItems(0) ?
    (
        List();
    )
    :
    (
        Info();
    );
);

main();
