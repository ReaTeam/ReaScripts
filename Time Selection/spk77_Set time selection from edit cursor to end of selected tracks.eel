// Set time selection from edit cursor to end of selected tracks
// EEL script by SPK77 1.7.2015
//
// Version: 2015.7.1

function set_time_sel_end() local(cursor_pos, max_pos, track_count, i, tr, j, item, item_end)
(
  cursor_pos = max_pos = GetCursorPosition(0);
  track_count = CountSelectedTracks(0);
  track_count > 0 ? (
    i = 0;
    loop(track_count,
      (tr = GetSelectedTrack(0, i)) ? (
        j = 0;
        loop(GetTrackNumMediaItems(tr),
          (item = GetTrackMediaItem(tr, j)) ? (
            item_end = GetMediaItemInfo_Value(item, "D_POSITION") + GetMediaItemInfo_Value(item, "D_LENGTH");
            max_pos < item_end ? max_pos = item_end;
          );
          j += 1;
        );
      );
      i += 1;
    );
  
    cursor_pos < max_pos ? (
      GetSet_LoopTimeRange(1, 0, cursor_pos, max_pos, 0);
      UpdateTimeline();
      Undo_OnStateChangeEx("Set time selection from edit cursor to end of selected tracks", -1, -1);
    );
  );
);

defer("set_time_sel_end()");
