/*
   * ReaScript Name: Create take pan envelope from active take to other takes in item
   * EEL script for Cockos REAPER
   * Author: spk77
   * Author URI: http://forum.cockos.com/member.php?u=49553
   * Licence: GPL v3
   * Version: 1.0
   */
  
  // Paste take pan envelope from active take in selected item to other takes in item 
// EEL script by spk77 20.7.2014
//
// Version: 0.2014.7.20

function paste_take_pan_env()
(
  CountSelectedMediaItems(0) == 1 && (source_take = GetActiveTake(GetSelectedMediaItem(0, 0))) ? (
    item = GetSelectedMediaItem(0, 0);
    (source_pan_env = GetTakeEnvelopeByName(source_take, "Pan")) ? (
      GetSetEnvelopeState(source_pan_env, #source_pan_env);
      
      Undo_BeginBlock();
      i = 0;
      loop(CountTakes(item),
        source_pan_env && source_take != GetTake(item, i) ? (
          SetActiveTake(GetTake(item, i));
          (env = GetTakeEnvelopeByName(GetTake(item, i), "Pan")) ? (
            GetSetEnvelopeState(env, #source_pan_env);
          ) : (
            Main_OnCommand(NamedCommandLookup("_S&M_TAKEENV2"), 0);
            (env = GetTakeEnvelopeByName(GetTake(item, i), "Pan")) ? (
              GetSetEnvelopeState(env, #source_pan_env);
            );
          );
        );
        i += 1;
      );
      SetActiveTake(source_take);
      Undo_EndBlock("Paste take pan envelope", -1);
    );
  ) : (
    ShowConsoleMsg("Please select only one (non-empty) item.");
  );
);

paste_take_pan_env();
