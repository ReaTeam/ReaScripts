/*
   * ReaScript Name: Paste stored take pitch envelope to active takes in same group
   * EEL script for Cockos REAPER
   * Author: spk77
   * Author URI: http://forum.cockos.com/member.php?u=49553
   * Licence: GPL v3
   * Version: 1.0
   */
  
  // Paste "stored" take pitch envelope to active takes in same group
// EEL script by spk77 20.7.2014
//
// Version: 0.2014.7.20

function paste_take_pitch_env() local(source_take, group, source_pitch_env, item_i, item, env)
(
  GetExtState(#retval, "take_envelope_source", "take");
  match("%d", #retval, source_take) ? (
    // if source_take is not in grouped item, show error message
    (group = GetMediaItemInfo_Value(GetMediaItemTake_Item(source_take), "I_GROUPID")) ? ( 
      (source_pitch_env = GetTakeEnvelopeByName(source_take, "Pitch")) ? (
        GetSetEnvelopeState(source_pitch_env, #source_pitch_env);
        
        Undo_BeginBlock();
        item_i = 0;
        group > 0 && loop(CountMediaItems(0),
          item = GetMediaItem(0, item_i);
          GetMediaItemInfo_Value(item, "I_GROUPID") == group && GetActiveTake(item) != source_take ? (
            (env = GetTakeEnvelopeByName(GetActiveTake(item), "Pitch")) ? (
              GetSetEnvelopeState(env, #source_pitch_env);
            ) : (
              isMediaItemSelected(item) ? (
                Main_OnCommand(NamedCommandLookup("_S&M_TAKEENV10"), 0);
              ) : (
                SetMediaItemSelected(item, 1);
                UpdateArrange();
                Main_OnCommand(NamedCommandLookup("_S&M_TAKEENV10"), 0);
                SetMediaItemSelected(item, 0);
              );
              (env = GetTakeEnvelopeByName(GetActiveTake(item), "Pitch")) ? (
                GetSetEnvelopeState(env, #source_pitch_env);
              );
            );
          );
          item_i += 1;
        );
        Undo_EndBlock("Paste take pitch envelope", -1);
      );
    ) : (
      ShowConsoleMsg("Stored take's parent item is not grouped");
    );
  );
);

paste_take_pitch_env();
